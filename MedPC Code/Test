\INPUTS: sent from box to program
^ActiveLever = 1
^InactiveLever = 2
^
\OUTPUTS: sent to box from program
^Tone1 = 9
^Tone2 = 10
^Tone3 = 11
^Tone4 = 12
^Light = 13
^Pump = 14
^Fan = 16
\VARIABLES:
\A = Active LeverPress
\B = Active LeverPress during TO
\C = InActive LeverPress
\D = Inactive LeverPress during TO
\E = Lick On
\F = Lick Off
\G = FR Counter
\H
\I
SEALED_ARRAY J = 2000     \Active LeverPress Timestamp
SEALED_ARRAY K = 2000     \Active LeverPress during TO
SEALED_ARRAY L = 2000     \InActive LeverPress Timestamp
SEALED_ARRAY M = 2000     \Inactive LeverPress during TO
SEALED_ARRAY N = 2000     \Lick On Timestamp
SEALED_ARRAY O = 2000     \Lick Off Timestamp
SEALED_ARRAY P = 2000     \Pump/Devlivery Timestamp
\Q
\R
\S
\T = Session Timer
\U
\V = Delivery Tracker
\W
\X
\Y
\Z
\STATE SETS
\STATE SET 1: FR controls
S.S.1,
S1,    \
       #START: ON ^Fan ---> S2
S2,    \
       #R^ActiveLever: SET J(A) = T; ADD A; ADD G; IF G = 2 [@Yes, @No]
                    @Yes: ON ^Pump; ON ^Light; ON ^Tone1; SET P(V) = T; ADD V ---> S3
                    @No: ---> SX
    #R^InactiveLever: SET L(C) = T; ADD C ---> SX
S3,    \
       0.5": OFF ^Pump; SET G = 0; OFF ^Tone1; OFF ^Light; IF V = 240 [@Yes, @No]
                    @Yes: ---> STOPSAVE
                    @No: ---> S4
       #R^ActiveLever: SET K(B) = T; ADD B ---> SX
    #R^InactiveLever: SET M(D) = T; ADD D ---> SX

S4,        \TO duration, in addition to pump time
     4.5": ---> S2
     #R^ActiveLever: SET K(B) = T; ADD B; ---> SX
    #R^InactiveLever: SET M(D) = T; ADD D ---> SX

\STATE SET 2: active timer and live med feed
S.S.5,
S1,    \use state set at the start of the program
       #START ---> S2
S2,    \update timer and trackers every millisecond in med
       0.01": SET T = T + 0.01; SHOW 1, Active LP, A, 2, Active TO LP, B, 3, Deliv, V, 4, Inactive LP, C, 5, Inactive TO LP, D, 6, Timer, T ---> SX

\STATE SET 3: program end
S.S.6,
S1,    \use state set at the start of the program
       #START ---> S2
S2,    \stop and save the data file after 30 minutes
       60' ---> STOPSAVE
